varnishtest "Test invalid input"

server s1 {
	rxreq
	txresp
} -start

varnish v1 -vcl+backend {
	import utf8 from "${vmod_topbuild}/src/.libs/libvmod_utf8.so";

	sub vcl_deliver {
		set resp.http.invalid = utf8.transform(
		    req.http.iso88591, 0);
		set resp.http.no-input1 = utf8.transform(
		    req.http.nonexistent, 0);
		set resp.http.no-input2 = utf8.transform(
		    "", 0);
	}
} -start

logexpect l1 -v v1 {
	expect * 1001	Error \
	    "utf8.transform: utf8proc_decompose: Invalid UTF-8 string"
	expect * =	Error	"utf8.transform: No input"
	expect * =	Error	"utf8.transform: No input"
} -start

client c1 {
	# This is ISO-8859-1 encoded
	txreq -hdr "iso88591: áéíóúçñßÁÉÍÓÚÇÑ"
	rxresp
	expect resp.http.invalid == ""
	expect resp.http.no-input1 == ""
	expect resp.http.no-input2 == ""
} -run

logexpect l1 -wait
